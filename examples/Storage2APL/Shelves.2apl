include: person.2apl;


beliefupdates:
	{ shelf(X, Y, free) }		AssignShelf(X, Y)	{ not shelf(X, Y, free), shelf(X, Y, assigned) }
	{ shelf(X, Y, assigned) }	FullShelf(X, Y)		{ not shelf(X, Y, assigned), shelf(X, Y, full) }
	{ shelf(X, Y, full) }		FreeShelf(X, Y)		{ shelf(X, Y, free), not shelf(X, Y, full) }


pcrules:
	event(wallAt(X, Y), blockworld) <- true |
	{
		+ shelf(X, Y, free);
	}
	
	message(im, request, La, On, assign(X, Y)) <- true |
	{
		adoptz(assignFreeShelf);
	}
	
	message(om, request, La, On, assign(X, Y)) <- true |
	{
		adoptz(assignFullShelf);
	}
	
	message(im, inform, La, On, full(X, Y)) <- true |
	{
		FullShelf(X, Y);
	}
	

pgrules:
	assignFreeShelf <- shelf(X, Y, free) |
	{
		AssignShelf(X, Y);
		send(im, inform, shelf(X, Y));
		dropgoal(assignFreeShelf);
	}
	
	assignFullShelf <- shelf(X, Y, full) |
	{
		FreeShelf(X, Y);
		send(om, inform, shelf(X, Y));
		dropgoal(assignFullShelf);
	}