include: person.2apl;

beliefs:
	start(15,15).
	state(waiting).
	clean_og(blockworld) :- not trap(14, 14).


beliefupdates:  	
  	{ state(waiting) } 		WaitForPickupStatus()	{ not state(waiting), state(waitForPickup) }
  	{ state(waitForPickup)}	WaitingStatus()			{ not state(waitForPickup), state(waiting) }


goals:
	clean_og(blockworld).


plans:
  B(start(X,Y));
  @blockworld( enter( X, Y, green ), _ );
  @blockworld( senseTraps(), TRAPS );
  if B(TRAPS = [[default, 14, 14] | REST]) then
  {
  	+ trap(14, 14);
  }
  

pgrules:
	clean_og( blockworld ) <- state(waiting) and trap(X, Y) |
	{
  		send(shelves, request, assign(X, Y));
  		send(rm, request, assign(X, Y));
  		LookingForShelfStatus();
  	}
  	
  	clean_og( blockworld ) <- trap(Xt, Yt) and target_shelf(Xs, Ys) and worker(RIDER) |
  	{
		send(RIDER, inform, takeProduct(Xs, Ys, Xt, Yt));
		- target_shelf(Xs, Ys);
		- worker(RIDER);
  	}


pcrules:
	event(trapAt(14, 14), blockworld) <- true |
	{
		+ trap(14, 14);
		adoptz(clean_og(blockworld));
	}
	
	message(shelves, inform, La, On, shelf(X, Y)) <- true | {
		+ target_shelf(X, Y);
	}
	
	message(rm, inform, La, On, assigned(RIDER)) <- true | {
		+ worker(RIDER);
	}
	
	message(RIDER, inform, La, On, done(Xs, Ys, Xt, Yt)) <- true | {
		send(rm, inform, free(RIDER));
		- trap(Xt, Yt);
		WaitingStatus();
	}
	